apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka-android'

apply from: "ktlint.gradle"

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 23 // Android 6.0 (Marshmallow)
        targetSdkVersion 30
        versionCode 1
        versionName "3.9.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'frollo-sdk-test'
        ]
        buildConfigField "String", "SDK_VERSION_NAME", "\"$versionName\""
        buildConfigField "int", "SDK_VERSION_CODE", "$versionCode"
    }
    buildTypes {
        // TODO: Configure proguard - debugging crash logs might be a problem due to obfuscation
        // TODO: Make sure to test release version after obfuscation, especially serializable classes
        release {
            debuggable false
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //consumerProguardFiles 'lib-proguard-rules.txt'
        }
        debug {
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'

        String sharedTestDir = 'src/sharedTest/kotlin'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
    kapt {
        arguments {
            // Print schemas for each version of room database
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
        reportUndocumented = true
        includeNonPublic = false
        skipDeprecated = false
        skipEmptyPackages = true

        includes = ['overview.md','changelog.md','README.md','extras.md','packages.md']

        packageOptions {
            prefix = "us.frollo.frollosdk.database"
            suppress = true
        }
        packageOptions {
            prefix = "us.frollo.frollosdk.keystore"
            suppress = true
        }
        packageOptions {
            prefix = "us.frollo.frollosdk.network"
            suppress = true
        }
        packageOptions {
            prefix = "us.frollo.frollosdk.preferences"
            suppress = true
        }
        packageOptions {
            prefix = "us.frollo.frollosdk.FrolloSDKInitProvider"
            suppress = true
        }
    }

    // TODO: Uncomment this when we upgrade dokka version.
    //  Commented this for now as we are not able to add project description markup files
    /*dokkaHtml {
        dokkaSourceSets {
            named("main") {
                reportUndocumented.set(true)
                includeNonPublic.set(false)
                skipDeprecated.set(false)
                skipEmptyPackages.set(true)

                // includes.from("overview.md", "changelog.md", "README.md", "extras.md","packages.md")
                includes.from("packages.md")

                perPackageOption {
                    matchingRegex.set("us.frollo.frollosdk.database")
                    suppress.set(true)
                }
                perPackageOption {
                    matchingRegex.set("us.frollo.frollosdk.keystore")
                    suppress.set(true)
                }
                perPackageOption {
                    matchingRegex.set("us.frollo.frollosdk.network")
                    suppress.set(true)
                }
                perPackageOption {
                    matchingRegex.set("us.frollo.frollosdk.preferences")
                    suppress.set(true)
                }
                perPackageOption {
                    matchingRegex.set("us.frollo.frollosdk.FrolloSDKInitProvider")
                    suppress.set(true)
                }
            }
        }
    }*/
}

buildscript {
    ext.kotlin_version_internal = '1.5.0'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version_internal"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.17"
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version_internal"

    // Testing
    testImplementation "junit:junit:4.13"
    testImplementation "org.mockito:mockito-core:3.0.0"
    testImplementation "com.squareup.okhttp3:mockwebserver:3.14.4"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test:rules:1.3.0"
    androidTestImplementation "androidx.test:core:1.3.0"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "org.mockito:mockito-android:3.0.0"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:3.14.4"
    androidTestImplementation "com.squareup.retrofit2:retrofit-mock:2.7.1"
    androidTestImplementation "com.jraska.livedata:testing:0.6.0"
    androidTestImplementation "com.jraska.livedata:testing-ktx:0.6.0"

    // Android KTX
    implementation "androidx.core:core-ktx:1.3.2"

    // Android Arch Components
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.room:room-runtime:2.3.0"
    implementation 'androidx.room:room-rxjava2:2.3.0'
    kapt "androidx.room:room-compiler:2.3.0"

    // Anko (https://github.com/Kotlin/anko)
    implementation "org.jetbrains.anko:anko:0.10.8"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.7.1"
    implementation "com.squareup.retrofit2:converter-gson:2.7.1"

    // ThreeTenABP - Date-Time
    implementation "com.jakewharton.threetenabp:threetenabp:1.2.2"

    // Android Debug SDKDatabase
    debugImplementation "com.amitshekhar.android:debug-db:1.0.4"

    // Cryptography - SpongyCastle
    implementation "com.madgag.spongycastle:core:1.58.0.0"
    implementation "com.madgag.spongycastle:prov:1.58.0.0"
    implementation "com.madgag.spongycastle:bcpkix-jdk15on:1.58.0.0"
    implementation "com.madgag.spongycastle:bcpg-jdk15on:1.58.0.0"

    // AppAuth
    implementation "net.openid:appauth:0.7.1"
}
